extends Node3D

var track_points : bool = false
var trail_timer : float = 0.0
var trail_resolution : float = 0.1
var apex := 0
var display_data: Dictionary = {
	"Distance": "---",
	"Carry": "---",
	"Offline": "---",
	"Apex": "---",
	"VLA": 0.0,
	"HLA": 0.0,
	"Speed": "---",
	"BackSpin": "---",
	"SideSpin": "---",
	"TotalSpin": "---",
	"SpinAxis": "---"
}
var raw_ball_data: Dictionary = {}
var last_display: Dictionary = {}


# Called when the node enters the scene tree for the first time.
func _ready() -> void:
	GlobalSettings.range_settings.camera_follow_mode.setting_changed.connect(set_camera_follow_mode)
	GlobalSettings.range_settings.surface_type.setting_changed.connect(_on_surface_changed)
	GlobalSettings.range_settings.ball_type.setting_changed.connect(_on_ball_type_changing)
	set_camera_follow_mode(GlobalSettings.range_settings.camera_follow_mode.value)
	_apply_surface_to_ball()


func _on_ball_type_changing(_value) -> void:
	# Temporarily disable camera follow before ball switch to avoid projection errors
	$PhantomCamera3D.follow_mode = PhantomCamera3D.FollowMode.NONE
	$PhantomCamera3D.follow_target = null


func _unhandled_input(event: InputEvent) -> void:
	if event.is_action_pressed("reset"):
		_reset_display_data()
		$RangeUI.set_data(display_data)


func _on_tcp_client_hit_ball(data: Dictionary) -> void:
	raw_ball_data = data.duplicate()
	_update_ball_display()

	# Re-enable camera follow if the setting is on
	if GlobalSettings.range_settings.camera_follow_mode.value:
		set_camera_follow_mode(true)


func _process(_delta: float) -> void:
	# Refresh UI during flight/rollout so carry/apex update live; distance updates only at rest.
	if $Player.get_ball_state() != GolfBall.BallState.REST:
		_update_ball_display()


func _on_golf_ball_rest(_ball_data) -> void:
	raw_ball_data = _ball_data.duplicate()
	# Show final shot numbers immediately on rest
	_update_ball_display()

	# Return camera to starting position if follow mode is enabled
	if GlobalSettings.range_settings.camera_follow_mode.value:
		var camera_reset_delay: float = GlobalSettings.range_settings.ball_reset_timer.value
		await get_tree().create_timer(camera_reset_delay).timeout
		reset_camera_to_start()

	if GlobalSettings.range_settings.auto_ball_reset.value:
		await get_tree().create_timer(GlobalSettings.range_settings.ball_reset_timer.value).timeout
		_reset_display_data()
		$RangeUI.set_data(display_data)
		$Player.reset_ball()
		return

	# No auto reset: leave final numbers visible

func set_camera_follow_mode(value) -> void:
	if value:
		$PhantomCamera3D.follow_target = $Player/Ball
		$PhantomCamera3D.follow_mode = PhantomCamera3D.FollowMode.FRAMED
	else:
		$PhantomCamera3D.follow_mode = PhantomCamera3D.FollowMode.NONE
		$PhantomCamera3D.follow_target = null

func reset_camera_to_start() -> void:
	# Disable follow mode first
	$PhantomCamera3D.follow_mode = PhantomCamera3D.FollowMode.NONE
	$PhantomCamera3D.follow_target = null

	# Wait a frame to ensure phantom camera has fully stopped following
	await get_tree().process_frame

	# Calculate camera position: ball start position + follow offset
	var ball := $Player/Ball
	var ball_start := Vector3(0.0, ball.START_HEIGHT, 0.0)
	var follow_offset: Vector3 = $PhantomCamera3D.follow_offset
	var start_pos := ball_start + follow_offset

	# Tween the PhantomCamera3D back to starting position
	var tween := create_tween()
	tween.set_trans(Tween.TRANS_CUBIC)
	tween.set_ease(Tween.EASE_IN_OUT)
	tween.tween_property($PhantomCamera3D, "global_position", start_pos, 1.5)

	await tween.finished

	# Reset ball to starting position so it's visible for next shot
	ball.position = ball_start
	ball.velocity = Vector3.ZERO
	ball.omega = Vector3.ZERO
	ball.state = GolfBall.BallState.REST

	# Keep follow mode disabled - it will re-enable when the next shot starts


func _on_range_ui_hit_shot(data: Dictionary) -> void:
	# For local injected shots, prime the display immediately with the payload data.
	raw_ball_data = data.duplicate()
	_update_ball_display()

	# Re-enable camera follow if the setting is on
	if GlobalSettings.range_settings.camera_follow_mode.value:
		set_camera_follow_mode(true)


func _apply_surface_to_ball() -> void:
	if $Player.has_node("Ball"):
		$Player/Ball.set_surface(GlobalSettings.range_settings.surface_type.value)


func _on_surface_changed(value) -> void:
	if $Player.has_node("Ball"):
		$Player/Ball.set_surface(value)


func _reset_display_data() -> void:
	raw_ball_data.clear()
	last_display.clear()
	display_data["Distance"] = "---"
	display_data["Carry"] = "---"
	display_data["Offline"] = "---"
	display_data["Apex"] = "---"
	display_data["VLA"] = 0.0
	display_data["HLA"] = 0.0
	display_data["Speed"] = "---"
	display_data["BackSpin"] = "---"
	display_data["SideSpin"] = "---"
	display_data["TotalSpin"] = "---"
	display_data["SpinAxis"] = "---"


func _update_ball_display() -> void:
	# Show distance continuously (updates during flight/rollout, final at rest)
	var show_distance: bool = true
	display_data = ShotFormatter.format_ball_display(raw_ball_data, $Player, GlobalSettings.range_settings.range_units.value, show_distance, display_data)
	last_display = display_data.duplicate()
	$RangeUI.set_data(display_data)
	
